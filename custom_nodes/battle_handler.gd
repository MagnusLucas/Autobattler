class_name BattleHandler
extends Node

signal player_won
signal enemy_won

const ZOMBIE_TEST_POSITIONS := [
	Vector2i(8, 6),
	Vector2i(7, 4),
	Vector2i(8, 3),
]

const ZOMBIE = preload("uid://dl84c0o5mnq5d")

@export var game_state: GameState
@export var game_area: PlayArea
@export var game_area_unit_grid: UnitGrid
@export var battle_unit_grid: UnitGrid

@onready var scene_spawner: SceneSpawner = $SceneSpawner


func _ready() -> void:
	game_state.changed.connect(_on_game_state_changed)

# TEST
func _input(event: InputEvent) -> void:
	if event.is_action_pressed("test2"):
		get_tree().call_group("player_units", "queue_free")
	if event.is_action_pressed("test1"):
		get_tree().call_group("enemy_units", "queue_free")


func _setup_battle_unit(unit_coords: Vector2i, unit: BattleUnit) -> void:
	unit.global_position = game_area.get_global_from_tile(unit_coords)
	unit.global_position.y -= Arena.QUARTER_CELL_SIZE.y
	unit.tree_exited.connect(_on_battle_unit_died)
	battle_unit_grid.add_unit(unit_coords, unit)


func _clear_up_fight() -> void:
	get_tree().call_group("player_units", "queue_free")
	get_tree().call_group("enemy_units", "queue_free")
	get_tree().call_group("units", "show")


func _prepare_fight() -> void:
	get_tree().call_group("units", "hide")
	
	for unit_coord: Vector2i in game_area_unit_grid.get_all_occupied_tiles():
		var unit: Unit = game_area_unit_grid.units[unit_coord]
		var new_unit := scene_spawner.spawn_scene(battle_unit_grid) as BattleUnit
		new_unit.add_to_group("player_units")
		new_unit.stats = unit.stats.duplicate()
		new_unit.stats.team = UnitStats.Team.PLAYER
		_setup_battle_unit(unit_coord, new_unit)
	
	for unit_coord: Vector2i in ZOMBIE_TEST_POSITIONS:
		var new_unit := scene_spawner.spawn_scene(battle_unit_grid) as BattleUnit
		new_unit.add_to_group("enemy_units")
		new_unit.stats = ZOMBIE
		new_unit.stats.team = UnitStats.Team.ENEMY
		_setup_battle_unit(unit_coord, new_unit)
	
	for unit in get_tree().get_nodes_in_group("player_units"):
		set_unit_target_enemy(unit)


func set_unit_target_enemy(unit: BattleUnit) -> void:
	var random_enemy: BattleUnit = get_tree().get_nodes_in_group("enemy_units").pick_random()
	var next_position := UnitNavigation.get_next_position(unit, random_enemy)
	unit.create_tween().tween_property(unit, "global_position", next_position, 0.5)


func _on_game_state_changed() -> void:
	match game_state.current_phase:
		GameState.Phase.PREPARATION:
			_clear_up_fight()
		GameState.Phase.BATTLE:
			_prepare_fight()


func _on_battle_unit_died() -> void:
	# Either the unit died
	# or the game is being closed
	if not get_tree() or game_state.current_phase != GameState.Phase.BATTLE:
		return
	
	if get_tree().get_node_count_in_group("player_units") == 0:
		game_state.current_phase = GameState.Phase.PREPARATION
		enemy_won.emit()
	if get_tree().get_node_count_in_group("enemy_units") == 0:
		game_state.current_phase = GameState.Phase.PREPARATION
		player_won.emit()
